---
layout: post
title: "Lie To Me"
description: "Angular and mock data magic with ngMockE2E"
category: development
tags: [angular, javascript, frontend, tricks,ngMockE2E]
---
{% include JB/setup %}
As an exclusively Front-End developer, it can be frustrating when your Back-End developer doesn't have data ready for your display. Angular provides a way to intercept http calls and return static (or even moderately dynamic) data in a way that doesn't require you to rewrite an entire service.


##The Pledge
--
On the project I'm on, there are two developers: A Back-End developer (Mark) and a Front-End Developer (me). Mark is solely responsible for the api and all of the crazy database manipulations and polarity-inverting magic that he does. I, on the other hand, am responsible for the entire Front-End application. 

The twist, however, is the fact that I've promised my client a dashboard (that has data that Mark didn't know he was going to need to provide) by the end of the week. 

##The Turn
--
In the past, you could create a small "data fixture" system that, instead of making an http request to a backend, you could just instantiate an object that contained the data you wanted like so:

{% highlight jade %}

	fixtures = angular.module('app.fixtures')
	.factory 'UserFixture', ->
		return {
			getUser: -> 
				firstName: 'Jeremy'
				lastName: 'Dowell'
				twitter: 'codevinsky'
				id: Math.floor(Math.random() * 100)
			getLastLogin: ->
				return new Date()
		}

{% endhighlight %}

Then, in your controller, you'd  inject the fixture and use it like normal

{% highlight jade %}

	controllers = angular.module 'app.controllers'
	controllers.controller('UserFixtureController', [
	  '$scope'
	  'UserFixture'
	  ($scope,$UserFixture) ->
	    $scope.method = "Fixture"
	    $scope.user = $UserFixture.getUser();
	    $scope.lastLogin = $UserFixture.getLastLogin()
	])

{% endhighlight %}

{% highlight jade %}

	// userPartial.jade
	h1 {{user.firstName}}
	h2 {{user.lastName}}
	a(ng-href="http://twitter.com/{{user.twitter}}) @{{user.twitter}}
	p User Id: {{user.id}}
	p Last Login: {{lastLogin | date:'shortDate'}}

{% endhighlight %}

Which would produce something that looks like: 

---
<h1>Jeremy</h1>
<h2>Dowell</h2>
<a href="http://twitter.com/codevinsky/">@codevinsky</a>
<p>User Id: 1234</p>
<p>Last Login: 9/12/13 12:34 PM</p>
---

But what happens when you want to test updating and creating new items or when your back-end guy has all the data setup and you're ready to hook everything in? You have to go back in and change the way you're doing everything. You have to spend time coding your repositories and services while you could be spending time on making everything pixel perfect.

Well, not anymore.

-----

## The Prestige
--
Introducing our hero, **ngMockE2E**. As of the writing of this article, you must include *angular-mocks.js* in your project for this to work.

You'll include this in your app's module definition just like any other angular service.

You will also want to create a module for your mock data

{% highlight jade %}

	#app.coffee
	angular.module('app.mocks', [])

	App = angular.module('app', [
	  'ngCookies'
	  'ngResource'
	  'ngRoute'
	  'ngMockE2E'
	  'app.controllers'
	  'app.mocks'
	])

{% endhighlight %}

Next, create a factory for each of your mock end points. For example, I have an api model called "user" that has some actions on it: 
- */api/user/* - Gets the active user
- */api/user/lastLogin* - Get the active user's last login time 
- */api/user/:id* - Gets a user by id
- */api/user/update* - Posts new info to a user

With these end points in mind, I've created the following methods on my fake user backend:
- getActiveUser()
- getUserById(id)
- getLastLogin()
- saveUser(data)

and my UserMock service should look like this.


{% highlight jade %}
	
	#userMocks.coffee

	mocks = angular.module('app.mocks')
	.factory 'UserMock', ->
		@user = 
			firstName: 'Jeremy'
			lastName: 'Dowell'
			twitter: 'codevinsky'
			id: Math.floor( Math.random() * 100 )
		return {
			getActiveUser: =>
				console.log 'getting active user'
				return @user
			getUserById: (id) =>
				console.log 'getting user by id:', id
				@user.id = id
				return @user
			getLastLogin:  ->
				console.log 'getting last login'
				return { lastLogin: new Date() }
			saveUser: (data) =>
				@user = data
				console.log 'saving user:', @user
				return @user
		}

{% endhighlight %}

Great. So, we've identified what we want to mock and how to mock it. Now let's plug it in and see if it works by testing out the basic *getActiveUser()* method of our user mock.

You'll need to inject *$httpBackend* as well as any mocks you've created in your app.run method (if you don't have an app.run method, now's a good time to add one between your app module instantiation and you app.config block)

{% highlight jade %}
	
	#app.coffee

	app.run ['$httpBackend','UserMock', ($httpBackend, $UserMock) -> 
		  

	  #Route for GET /api/user/
	  $httpBackend.whenGET('/api/user').respond($UserMock.getActiveUser())
		  
	]

{% endhighlight %}

The first route we're testing is */api/user/* which will return our active user (or in the case of mock data, just the @user object)

To test this route, we need to make an *$http.get()* request to the url. In the UserRepository service, I've got a method that does just that

{% highlight jade %}
	
	#repositories.coffee
	app.factory 'UserRepository', ['$http', ($http) ->
		getActiveUser: ->
			$http.get('/api/user')
		getLastLogin: ->
			$http.get('/api/user/lastLogin')
		getUserById: (id) ->
			$http.get('/api/user/' + id)
		saveUser: (user) ->
			$http.post('/api/user/update', user)
	]

{% endhighlight %}

An observant eye will notice that the *methodNames* in my user repository match that of my user mock. That's simply to make sure I have the proper calls matching to the proper fake endpoints.

Now, in our controller, we'll want to ask the UserRepository for the active user.

{% highlight jade %}

	#controllers.coffee
	app.controller('UserMockController', [
	  '$scope'
	  '$routeParams'
	  'UserRepository'
	  ($scope, $routeParams, $UserRepository) ->
	    $UserRepository.getActiveUser().then (response) ->
	      console.log 'got active user:', response
	      $scope.user = response.data
	])

{% endhighlight %}
In the console, you should see something that looks like an actual XMLHttpRequest:

{% highlight bash %}
	got active user: Object {data: Object, status: 200, headers: function, config: Object}
{% endhighlight %}
The *data* object contains our active user data. 

**Awesome**

---
But that's child's play. Let's do something a bit more complicated. Let's use a dynamic route as a possible url

{% highlight jade %}

	#app.coffee

	app.run ['$httpBackend','UserMock', ($httpBackend, $UserMock) -> 
	  
	  . . .
	  # Route for GET /api/user/:id
	  $httpBackend.whenGET(/\/api\/user\/(\d+)$/).respond (method, url, data, header) ->
	    id = url.match(/(\d+)$/)[0]
	    data = $UserMock.getUserById(id)
	    return [
	      200, # status code
	      data # data object
	 	]
	  . . . 
	]

{% endhighlight %}

Oooh. That looks a bit more scary. Not to worry, we got this. This method matches a *GET* request to a url that matches the following pattern: 

{% highlight bash %}

	/api/user/(\d+)$

{% endhighlight %}
That is, you could send an http get request to */api/user/5123* and this route would match.

In the previous example, we didn't need to modify any data, or make decisions based on the url, so we simply passed our UserMock.getActiveUser() method back to *$httpBackend.whenGET*'s *respond* method.

This time, however, we've got some decision making to do (what is the ID that we are going to pass to our mock data?). Because of this, we must return an anonymous method that returns an array that represents an HTTP response's attributes back to *$httpBackend.whenGET*'s *respond* method. 

Re-read that statement, as it's a hairy one. 

Go on.

I'll wait.

Ok. Moving on.

The first thing that this anonymous method does is suss out the id from the url (using a similar matching pattern), and passing that as an id to the UserMock.getUserById(). That's all pretty straight forward, but it's the return that can be confusing without context. The $httpBackend service's *when* methods all expect an array to be returned. The first element in the array is the HTTP Status Code, the second element is the data object, and the third is the header object. The header object -seems- to be optional, which is why I've not included it here. 

So, if you need to do any decision making before hitting the Mock service, you'll need to follow this method.

The Mock methods:
{% highlight jade %}

	#userMock.coffee
	. . .

	getUserById: (id) =>
		console.log 'getting user by id:', id
			@user.id = id
			return @user
	. . .

{% endhighlight %}

I'm just grabbing the local user object and changing the id to be equal to the passed in id.



And the controller:

{% highlight jade %}

	#controllers.coffee
	app.controller('UserMockController', [
	  '$scope'
	  '$routeParams'
	  'UserRepository'
	  ($scope, $routeParams, $UserRepository) ->
	    $UserRepository.getUserById(1234).then (response) ->
	      console.log 'got user:', response
	      $scope.user = response.data
	])

{% endhighlight %}
The console output should look basically identical to the previous log


-----
Alright, I know you're tired of reading, and believe me, I'm tired of writing. But we've got one more case to show off. 

**CRUD**


ngMockE2E has all of the following http request method's built into it: GET, POST, PUT, DELETE, HEAD, JSONP, and PATCH. 

Here's a quick look at what a POST request looks like using $httpBackend

{% highlight jade %}

	#app.coffee
	app.run ['$httpBackend','UserMock', ($httpBackend, $UserMock) -> 
		. . .
		# Route for POST /api/user/update
		$httpBackend.whenPOST('/api/user/update').respond (method, url, data, header) ->
	    	return [200, $UserMock.saveUser(data) ]
		. . .

{% endhighlight %}

{% highlight jade %}

	#userMock.coffee
	. . .
	saveUser: (data) =>
		@user = data
		console.log 'saving user:', @user
		return @user
	. . .

{% endhighlight %}

{% highlight jade %}

	#controllers.coffee 
	. . .
	$scope.update = ->
	      $UserRepository.saveUser($scope.user).then (response) ->
	        console.log 'saved user:', response
	        $scope.user = response.data
	. . .

{% endhighlight %}

Now, we are -actually- posting to an http endpoint, but intercepting it with angular.

####So what happens when the endpoint become available for actual requests?
Simple. Just delete (or if you're paranoid like me, comment out) the $httpBackend.when method for the given url. If you and your back-end guy have correctly planned and agreed to a proper data contract, you'll see no change in behavior, save for the fact you'll be using live data.

----

## Source
Full demo & source on github: [gh:codevinsky/lie-to-me](http://github.com/codevinsky/lie-to-me)

---


